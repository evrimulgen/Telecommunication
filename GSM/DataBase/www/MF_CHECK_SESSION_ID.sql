CREATE OR REPLACE PROCEDURE MF_CHECK_SESSION_ID(
  pSESSION_KEY IN VARCHAR2,
  pUSER_ID OUT MF_SESSIONS.SESSION_ID%TYPE,
  pPREV_ACCESS_DATE_TIME OUT DATE
  ) IS
--
--#Version=1
--
CURSOR cFIND_SESSION(pSESSION_KEY VARCHAR2) IS
  SELECT
    LAST_ACCESS_DATE_TIME,
    EXPIRED_FLAG,
    USER_ID,
    PREV_ACCESS_DATE_TIME
  FROM MF_SESSIONS
  WHERE SESSION_KEY=pSESSION_KEY
  FOR UPDATE OF LAST_ACCESS_DATE_TIME
    ;
--
  vLAST_ACCESS_DATE_TIME DATE;
  vEXPIRED_FLAG BINARY_INTEGER;
  cSESSION_TIMEOUT CONSTANT NUMBER := 0.5; -- 12 часов - для отладки
--  SESSION_TIMEOUT CONSTANT NUMBER := 0.01388889; -- 20 минут
--
BEGIN
  OPEN cFIND_SESSION(pSESSION_KEY);
  FETCH cFIND_SESSION INTO vLAST_ACCESS_DATE_TIME, vEXPIRED_FLAG, pUSER_ID, pPREV_ACCESS_DATE_TIME;
  IF cFIND_SESSION%FOUND THEN
    IF (vEXPIRED_FLAG <> 0) OR (SYSDATE()-vLAST_ACCESS_DATE_TIME > cSESSION_TIMEOUT) THEN
      pUSER_ID := 0;
      pPREV_ACCESS_DATE_TIME := NULL;
    ELSE
      IF vLAST_ACCESS_DATE_TIME <> SYSDATE THEN
        UPDATE MF_SESSIONS
        SET LAST_ACCESS_DATE_TIME=SYSDATE
        WHERE CURRENT OF cFIND_SESSION;
      END IF;
    END IF;
    COMMIT; -- Чтобы немедленно разблокировать строку (а надо ли?)
  ELSE
    pUSER_ID := NULL;
    pPREV_ACCESS_DATE_TIME := NULL;
  END IF;
  CLOSE cFIND_SESSION;
END MF_CHECK_SESSION_ID;
/
