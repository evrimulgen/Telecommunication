CREATE OR REPLACE FUNCTION K7_LK.RESET_PASSWORD(
  pLOGIN IN VARCHAR2
  ) RETURN VARCHAR2 IS
--
--#Version=3
--
--v.3 26.11.2015 Афросин Переделал текст смс 
--
-- Функция сбрасывает пароль для телефона и отправляет его на SMS.
-- Возвращаемое значение:
--  'OK': операция выполнена успешно;
--  'NO_PHONE': нет такого номера;
--  'ERROR': Ошибка, повторить позже.
--
vCONTRACT_ID INTEGER;
vNEW_PASSWORD CORP_MOBILE.CONTRACTS.USER_PASSWORD%TYPE;
vRESULT VARCHAR2(20);
CURSOR C_CONTRACT IS
   SELECT
      CONTRACTS.CONTRACT_ID
   FROM CORP_MOBILE.CONTRACTS,
        CORP_MOBILE.CONTRACT_CANCELS CCS
   WHERE     CONTRACTS.PHONE_NUMBER_FEDERAL = pLOGIN
          AND CCS.CONTRACT_ID(+)=CONTRACTS.CONTRACT_ID
          AND CCS.CONTRACT_CANCEL_DATE IS NULL;
BEGIN
  IF PHONE_CAN_LOG_IN(pLOGIN) THEN
    OPEN C_CONTRACT;
    FETCH C_CONTRACT INTO vCONTRACT_ID;
    CLOSE C_CONTRACT;
    vNEW_PASSWORD := TRUNC(100000+ dbms_random.value() * 900000);
    UPDATE CORP_MOBILE.CONTRACTS
      SET USER_PASSWORD=vNEW_PASSWORD
      WHERE CONTRACT_ID=vCONTRACT_ID;
    IF CORP_MOBILE.LOADER3_PCKG.SEND_SMS(pLOGIN,
      'ag-sv.ru',
      'Для входа в личный кабинет ag-sv.ru новый пароль: ' || vNEW_PASSWORD
      ) IS NULL THEN
      vRESULT := 'OK';
    ELSE
      vRESULT := 'ERROR';
    END IF;
    INSERT INTO PASSWORD_RESET_LOGS (
      ID, USER_ID, SMS_SEND_RESULT, CREATED_AT, UPDATED_AT
    ) VALUES (
      PASSWORD_RESET_LOGS_SEQ.NEXTVAL, vCONTRACT_ID, vRESULT, SYSDATE, SYSDATE
    );
  ELSE
    vRESULT := 'NO_PHONE';
  END IF;
  RETURN vRESULT;
END;
/

GRANT UPDATE(USER_PASSWORD) ON CONTRACTS TO K7_LK;

GRANT EXECUTE ON LOADER3_PCKG TO K7_LK;
