
  CREATE OR REPLACE FUNCTION "CORP_MOBILE"."GET_TARIFF_OPTION_ADD_COST_TES" (
  pACCOUNT_ID IN INTEGER,
  pYEAR_MONTH IN INTEGER,
  pPHONE_NUMBER IN VARCHAR2
  ) RETURN NUMBER IS
--Version=1
  CURSOR C IS
    SELECT PO.TURN_ON_DATE,
           PO.TURN_OFF_DATE,
           TC.MONTHLY_COST,
           TC.OPERATOR_MONTHLY_COST,
           TC.TURN_ON_COST,
           TC.OPERATOR_TURN_ON_COST,
           PO.OPTION_CODE,
           V.DATE_BEGIN,
           V.DATE_END,
           V.TARIFF_ID,
           TC.TARIFF_OPTION_COST_ID, 
           op.DISCR_SPISANIE
      FROM DB_LOADER_ACCOUNT_PHONE_OPTS PO,
           V_BILL_ABON_PER_FOR_CLIENT V,
           TARIFF_OPTIONS OP,
           TARIFF_OPTION_COSTS TC
      WHERE PO.ACCOUNT_ID = pACCOUNT_ID
        AND PO.YEAR_MONTH = pYEAR_MONTH
        AND PO.PHONE_NUMBER = pPHONE_NUMBER
        AND V.ACCOUNT_ID = PO.ACCOUNT_ID
        AND V.YEAR_MONTH = PO.YEAR_MONTH
        AND V.PHONE_NUMBER = PO.PHONE_NUMBER
        AND V.TARIFF_CODE IS NOT NULL
        AND PO.OPTION_CODE = OP.OPTION_CODE
        AND OP.TARIFF_OPTION_ID = TC.TARIFF_OPTION_ID
        AND TC.BEGIN_DATE <= V.DATE_BEGIN
        AND TC.END_DATE >= V.DATE_END;
  CURSOR NC(pTARIFF_ID IN INTEGER, pTARIFF_OPTION_COST_ID IN INTEGER) IS
    SELECT *
      FROM TARIFF_OPTION_NEW_COST NC
      WHERE NC.TARIFF_ID = pTARIFF_ID
        AND NC.TARIFF_OPTION_COST_ID = pTARIFF_OPTION_COST_ID;
  vDUMMY NC%ROWTYPE;   
  ITOG NUMBER(13, 2);    
  vBEGIN DATE;
  vEND DATE;         
  vBEGIN_YM DATE;
  vEND_YM DATE;   
  vTURN_ON_COST NUMBER(15, 2);  
  vMONTHLY_COST NUMBER(15, 2);    
BEGIN 
  ITOG:=0;
  vBEGIN_YM:=TO_DATE(TO_CHAR(pYEAR_MONTH)||'01', 'YYYYMMDD');
  vEND_YM:=LAST_DAY(vBEGIN_YM);
  FOR rec IN C LOOP
    OPEN NC(rec.TARIFF_ID, rec.TARIFF_OPTION_COST_ID);
    FETCH NC INTO vDUMMY;
    IF NC%FOUND THEN
      vTURN_ON_COST:=NVL(vDUMMY.TURN_ON_COST_FOR_BILLS, rec.TURN_ON_COST);
      vMONTHLY_COST:=NVL(vDUMMY.MONTHLY_COST_FOR_BILLS, rec.MONTHLY_COST);   
    ELSE
      vTURN_ON_COST:=rec.TURN_ON_COST;
      vMONTHLY_COST:=rec.MONTHLY_COST; 
    END IF;
    CLOSE NC;
    vBEGIN:=rec.TURN_ON_DATE;
    IF vBEGIN < rec.DATE_BEGIN THEN
      vBEGIN:=rec.DATE_BEGIN;
    END IF;
    vEND:=NVL(rec.TURN_OFF_DATE, TO_DATE('20501231', 'YYYYMMDD'));
    IF vEND > rec.DATE_END THEN
      vEND:=rec.DATE_END;
    END IF;
    IF vTURN_ON_COST <> rec.OPERATOR_TURN_ON_COST THEN
      IF (rec.TURN_ON_DATE >= vBEGIN) AND (rec.TURN_ON_DATE <= vEND) THEN
        ITOG:=ITOG + vTURN_ON_COST - rec.OPERATOR_TURN_ON_COST;
      END IF;
    END IF;
    IF vMONTHLY_COST <> rec.OPERATOR_MONTHLY_COST THEN
      if nvl(rec.DISCR_SPISANIE, 0) = 0 then
        ITOG:=ITOG + (vMONTHLY_COST - rec.OPERATOR_MONTHLY_COST)*(vEND - vBEGIN + 1)/(vEND_YM - vBEGIN_YM + 1);
      else
        ITOG:=ITOG + (vMONTHLY_COST - rec.OPERATOR_MONTHLY_COST);
      end if;
    END IF;
  END LOOP;
  RETURN ITOG;
END; 