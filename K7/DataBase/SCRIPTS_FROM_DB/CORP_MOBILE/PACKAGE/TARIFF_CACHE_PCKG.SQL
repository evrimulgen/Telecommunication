
  CREATE OR REPLACE PACKAGE "CORP_MOBILE"."TARIFF_CACHE_PCKG" IS
--#Version=1
--
-- Позволяет быстро получить ID тарифа по коду
-- Возвращает массив кодов
--
  FUNCTION GET_TARIFFS(pTARIFF_CODE IN VARCHAR2) RETURN DBMS_SQL.NUMBER_TABLE;
END; 
CREATE OR REPLACE PACKAGE BODY "CORP_MOBILE"."TARIFF_CACHE_PCKG" IS
--#Version=1
--
-- храним кэш кодов.
-- Устаревает каждые 10 секунд
  TYPE T_CACHE IS TABLE OF DBMS_SQL.NUMBER_TABLE INDEX BY VARCHAR2(1000);
  gCACHE T_CACHE;
  gCACHE_EXPIRED DATE;
--
-- Позволяет быстро получить ID тарифа по коду
-- Возвращает массив кодов
  FUNCTION GET_TARIFFS(pTARIFF_CODE IN VARCHAR2) RETURN DBMS_SQL.NUMBER_TABLE IS
    CURSOR C_TARIFFS IS
    SELECT TARIFF_ID
      FROM TARIFFS WHERE TARIFF_CODE=pTARIFF_CODE
      ORDER BY NVL(TARIFFS.TARIFF_PRIORITY, 9999) ASC;
    vRESULT DBMS_SQL.NUMBER_TABLE;
  BEGIN
    IF gCACHE_EXPIRED IS NULL OR gCACHE_EXPIRED < SYSDATE THEN
      gCACHE_EXPIRED := SYSDATE + 10/60/60/24;
      gCACHE.DELETE;
    END IF;
    IF gCACHE.EXISTS(pTARIFF_CODE) THEN
      RETURN gCACHE(pTARIFF_CODE);
    ELSE
      OPEN C_TARIFFS;
      FETCH C_TARIFFS BULK COLLECT INTO vRESULT;
      CLOSE C_TARIFFS;
      gCACHE(pTARIFF_CODE) := vRESULT;
      RETURN vRESULT;
    END IF;
  END;
END; 