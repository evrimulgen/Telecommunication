
  CREATE OR REPLACE PROCEDURE "CORP_MOBILE"."SAVE_CONTRACT_PAYMENT" (
  pCONTRACT_ID IN INTEGER,
  pPAYMENT_SUM IN NUMBER
  ) IS
--#Version=1
--
  CURSOR C_FIND IS
  SELECT 
    RECEIVED_PAYMENTS.ROWID,
    RECEIVED_PAYMENTS.PAYMENT_SUM,
    RECEIVED_PAYMENTS.PHONE_NUMBER,
    RECEIVED_PAYMENTS.FILIAL_ID
  FROM RECEIVED_PAYMENTS
  WHERE
    RECEIVED_PAYMENTS.CONTRACT_ID=pCONTRACT_ID
    AND RECEIVED_PAYMENTS.IS_CONTRACT_PAYMENT = 1;
  C_REC C_FIND%ROWTYPE;
  vCONTRACT_DATE DATE;
  vPHONE_NUMBER CONTRACTS.PHONE_NUMBER_FEDERAL%TYPE;
  vFILIAL_ID CONTRACTS.FILIAL_ID%TYPE;
BEGIN
  SELECT 
    CONTRACT_DATE,
    FILIAL_ID,
    PHONE_NUMBER_FEDERAL
  INTO
    vCONTRACT_DATE,
    vFILIAL_ID,
    vPHONE_NUMBER
  FROM CONTRACTS
    WHERE CONTRACTS.CONTRACT_ID=pCONTRACT_ID;
  OPEN C_FIND;
  FETCH C_FIND INTO C_REC;
  IF C_FIND%NOTFOUND THEN
    INSERT INTO RECEIVED_PAYMENTS (
      RECEIVED_PAYMENT_ID,
      PHONE_NUMBER,
      PAYMENT_SUM,
      CONTRACT_ID,
      FILIAL_ID,
      PAYMENT_DATE_TIME,
      IS_CONTRACT_PAYMENT
    ) VALUES (
      NEW_RECEIVED_PAYMENT_ID,
      vPHONE_NUMBER,
      pPAYMENT_SUM,
      pCONTRACT_ID,
      vFILIAL_ID,
      vCONTRACT_DATE,
      1
    );
  ELSE
    IF NVL(C_REC.PHONE_NUMBER, '-PPP') <> NVL(vPHONE_NUMBER, '-PPP')
      OR NVL(C_REC.PAYMENT_SUM, -1001.001) <> NVL(pPAYMENT_SUM, -1001.001)
      OR NVL(C_REC.FILIAL_ID, -1001.001) <> NVL(vFILIAL_ID, -1001.001) THEN
      -- Условия платежа изменились
      -- Аннулируем старый платёж
      UPDATE RECEIVED_PAYMENTS
      SET 
        PAYMENT_ANNUL_FLAG = 1, 
        PAYMENT_ANNUL_DATE_TIME = SYSDATE, 
        PAYMENT_ANNUL_REASON = 'Изменение договора', 
        USER_WHO_ANNULATE = USER
      WHERE
        ROWID=C_REC.ROWID
        ;
      --  И добавим новый платёж
      INSERT INTO RECEIVED_PAYMENTS (
        RECEIVED_PAYMENT_ID,
        PHONE_NUMBER,
        PAYMENT_SUM,
        CONTRACT_ID,
        FILIAL_ID,
        PAYMENT_DATE_TIME,
        IS_CONTRACT_PAYMENT
      ) VALUES (
        NEW_RECEIVED_PAYMENT_ID,
        vPHONE_NUMBER,
        pPAYMENT_SUM,
        pCONTRACT_ID,
        vFILIAL_ID,
        vCONTRACT_DATE,
        1
      );
    END IF;
  END IF;
  CLOSE C_FIND;
END; 